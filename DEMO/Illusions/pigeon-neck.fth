\ optical illusion using Sprites 

INCLUDE DSK1.AUTOMOTION 
INCLUDE DSK1.CHARSET 

DECIMAL 
  5 CONSTANT BLU
  3 CONSTANT SPEED  
128 CONSTANT BAR 

: CHARACTERS 
S" CCCCCCCCCCCCCCCC" BAR CALLCHAR  
S" 0000000000000000000000000000000000000000000000000000FCFCFCFCFFFF"
88 CALLCHAR
( 120)
S" 000003030F0F3F3FFFFF000000000000FCFCFCFCFCFCFCFCFCFCC0C0C0C0C0F0" 
92 CALLCHAR
( 130)
S" 7F7F0000000000000000000000000000FFFF0000000000000000000000000000" 
96 CALLCHAR ;

: 64*  8* 8* ;

: PIGEONS 
 4 0 
 DO  
\ char colr  x            y            spr#
   88  BLU  I 64* 17 +   I 23 * 1+      I 2*    SPRITE 
   92  BLU  I 64* 15 +   I 23 * 33 +    I 2* 1+ SPRITE
 LOOP ; 

: BARS 
  4 0 DO 
\ char colr    x            y          spr#
   96  BLU  I 64* 15 +   I 23 * 80 +   I 8 + SPRITE 
  LOOP 
; 

: VERTICALS 
   BAR SET# 2 1 COLOR 
   0 C/SCR @ BAR VFILL 
; 

: VISIBLE    BAR SET#  1  2 COLOR ;
: INVISIBLE  BAR SET# 16 16 COLOR ;

: ILLUSION 
  BEGIN 
    VISIBLE   4000 MS 
    INVISIBLE 3000 MS 
    ?TERMINAL  
  UNTIL ;

 : MOVE-EM  
   12 MOVING 
   12 0 DO  SPEED 0 I MOTION  LOOP ; 

: RUN
    GRAPHICS AUTOMOTION 
    CLEAR  16 SCREEN  3 MAGNIFY 
    CHARACTERS  VERTICALS 
    PIGEONS  BARS 
    MOVE-EM 
    ILLUSION
    CHARSET  8 SCREEN   DELALL 
    CLEAR 
;

