\ Color Bars for TI-99 CAMEL99 Forth

NEEDS DUMP    FROM DSK1.TOOLS
NEEDS HCHAR   FROM DSK1.GRAFIX
NEEDS CHARSET FROM DSK1.CHARSET
\ Name TI-99 colors
1 CONSTANT CLR     2 CONSTANT BLK    3 CONSTANT MGRN   4 CONSTANT LGRN
5 CONSTANT BLU     6 CONSTANT LBLU   7 CONSTANT RED    8 CONSTANT CYAN
9 CONSTANT MRED   10 CONSTANT LRED  11 CONSTANT YEL   12 CONSTANT LYEL
13 CONSTANT GRN   14 CONSTANT MAG   15 CONSTANT GRY   16 CONSTANT WHT


DECIMAL
: DEFCHARS ( pattern first last -- )
        1+ SWAP ?DO   DUP I CHARDEF   8 +LOOP DROP ;

\ square character data
 HEX
 CREATE SQUARE  FFFF , FFFF , FFFF , FFFF , 0 ,

DECIMAL
: COLOR-BARS ( -- )
   24 0 DO
\   col row char wid
\   --- --- ---- ---
      2  I   88   4 HCHAR
      6  I   96   4 HCHAR
     10  I  104   4 HCHAR
     14  I  112   4 HCHAR
     18  I  120   4 HCHAR
     22  I  128   4 HCHAR
     26  I  136   4 HCHAR
   LOOP
;

: SET-COLORS ( -- )
\   charset  fg   bg
\   -------  --   --
    88 SET#  GRY  CLR COLOR
    96 SET#  YEL  CLR COLOR
   104 SET#  CYAN CLR COLOR
   112 SET#  GRN  CLR COLOR
   120 SET#  MAG  CLR COLOR
   128 SET#  RED  CLR COLOR
   136 SET#  BLU  CLR COLOR
   144 SET#  BLK  CLR COLOR
;



\ restore characters and colors
: DEFAULTS
   8 SCREEN
   4 19 BLK CLR COLORS
   CLEAR
   CHARSET ;

: BARS
   CLEAR  BLK SCREEN
   SET-COLORS
   SQUARE 88 152 DEFCHARS
   COLOR-BARS
   BEGIN ?TERMINAL  UNTIL
   DEFAULTS
;
CR
CR .( Type BARS to run. CTRL BREAK Stop)
