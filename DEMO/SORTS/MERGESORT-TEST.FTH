

1 CELLS CONSTANT CELL
: <=     1+ < ;

: MERGE-STEP ( RIGHT MID LEFT -- RIGHT MID+ LEFT+ )
  OVER @ OVER @ <
  IF
    OVER @ >R
    2DUP - OVER DUP CELL+ ROT MOVE
    R> OVER !
    >R CELL+ 2DUP = IF R> DROP DUP ELSE R> THEN
  THEN CELL+ ;

: MERGE ( RIGHT MID LEFT -- RIGHT LEFT )
  DUP >R BEGIN 2DUP > WHILE MERGE-STEP REPEAT 2DROP R> ;

: MID ( L R -- MID ) OVER - 2/ CELL NEGATE AND + ;

: MERGESORT ( RIGHT LEFT -- RIGHT LEFT )
  2DUP CELL+ <= IF EXIT THEN
  SWAP 2DUP MID RECURSE ROT RECURSE MERGE ;

: SORT ( ADDR LEN -- )  CELLS OVER + SWAP MERGESORT 2DROP ;

CREATE TEST 8 , 1 , 5 , 3 , 9 , 0 , 2 , 7 , 6 , 4 , 0 , 0 ,

: .ARRAY ( ADDR LEN -- ) 0 DO DUP I CELLS + @ . LOOP DROP ;

TEST 10 2DUP SORT .ARRAY       \ 0 1 2 3 4 5 6 7 8 9