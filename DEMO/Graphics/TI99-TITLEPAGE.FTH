\ BIG chars from GROM with no Assembler
\ this program reads the LARGE FONT from Graphics ROM (GROM)
\ that are used in the TI-99 title screen.

INCLUDE DSK1.GRAFIX
INCLUDE DSK1.CHARSET    \ sets BIG caps or standard charset

HERE
\ named colors for Graphics programs
: ENUM  ( 0 <text> -- n) DUP CONSTANT  1+ ;

1 ENUM TRANS   ENUM BLACK   ENUM MEDGRN ENUM LTGRN
  ENUM DKBLU   ENUM LTBLU   ENUM DKRED  ENUM CYAN
  ENUM MEDRED  ENUM LTRED   ENUM DKYEL  ENUM LTYEL
  ENUM DKGRN   ENUM MAGENTA ENUM GRAY   ENUM WHITE
DROP

HEX
CREATE BLK  FFFF , FFFF , FFFF , FFFF ,

: DEF-BLOCK ( char -- char ) BLK OVER CHARDEF ;

DECIMAL
: DEF-BLOCKS
    128 DEF-BLOCK    8 + DEF-BLOCK   8 + DEF-BLOCK
    8 + DEF-BLOCK    8 + DEF-BLOCK   8 + DEF-BLOCK
    8 + DEF-BLOCK    8 + DEF-BLOCK   8 + DEF-BLOCK
    8 + DEF-BLOCK    8 + DEF-BLOCK   8 + DEF-BLOCK
    8 + DEF-BLOCK    8 + DEF-BLOCK   8 + DEF-BLOCK
    8 + DEF-BLOCK
    DROP
;

: FG-COLOR  1 COLOR ;
: NEXTSET ( n -- n') 8 + DUP SET# ;

: SET-COLORS
  128 DUP SET# DKRED FG-COLOR
  NEXTSET LTGRN FG-COLOR
  NEXTSET BLACK FG-COLOR
  NEXTSET LTYEL FG-COLOR
  NEXTSET DKGRN FG-COLOR
  NEXTSET MAGENTA FG-COLOR
  NEXTSET WHITE FG-COLOR
  NEXTSET DKBLU FG-COLOR
  NEXTSET MEDGRN FG-COLOR
  NEXTSET MAGENTA FG-COLOR
  NEXTSET MEDRED FG-COLOR
  NEXTSET GRAY  FG-COLOR
  NEXTSET LTBLU FG-COLOR
  NEXTSET LTRED FG-COLOR
  NEXTSET DKYEL FG-COLOR
  NEXTSET DKRED FG-COLOR
  DROP
;

HEX
\ Load TI title page font from GROM to VDP
: BIGCAPS  ( -- ) 4B4 900 200 GVMOVE ;

DECIMAL
: (c)   10 EMIT ;

\ LOGO chars as short strings
CREATE LOGO 1 C, 2 C, 3 C,
            4 C, 5 C, 6 C,
            7 C, 8 C, 9 C,

: TI.LOGO ( -- )
  [ HEX ]
( Load LOGO from GROM to VDP )
\    GROM     VDP     9 patterns
\   ------  -------   ----------
     950     1 ]PDT   9 8*  GVMOVE   \ GROM->VDP move
  [ DECIMAL ]
    14 5 AT-XY   LOGO 3 TYPE
    14 6 AT-XY [ LOGO 3 + ] LITERAL 3 TYPE
    14 7 AT-XY [ LOGO 6 + ] LITERAL 3 TYPE
;

: .TITLE
     8  9 AT-XY     ." TEXAS INSTRUMENTS"
    10 11 AT-XY       ." HOME COMPUTER"
    10 13 AT-XY       ." CAMEL99 FORTH"
     2 15 AT-XY ." READY-PRESS ANY KEY TO BEGIN"
;

: .BAR
  128 DUP 15 8* + SWAP
  DO
    I DUP (EMIT) (EMIT)
  8 +LOOP
  248 DUP (EMIT) VPUT \ last char does not inc VCOL
;

: .BANNER  .BAR CR .BAR CR .BAR ;

: VC+!  ( n Vaddr) S" DUP>R VC@ +  R> VC!" EVALUATE ; IMMEDIATE

HEX
: KALEIDOSCOPE
   10 ]CTAB 10
   BOUNDS
   DO
      10 I VC+!
      10 TICKS
   LOOP
;

DECIMAL
: TITLEPAGE
    GRAPHICS
    CYAN SCREEN
    BIGCAPS DEF-BLOCKS SET-COLORS
    PAGE
    .BANNER
    TI.LOGO
    .TITLE
    CR CR  .BANNER
    7 22 AT-XY (c) ."  2025" 2 SPACES ." BRIAN FOX"
    2000 MS
    BEGIN KALEIDOSCOPE KEY? UNTIL
    TEXT CHARSET
;

DECIMAL
HERE SWAP - . .( bytes)
