\ ===========================================================================
\  T M S 9 9 0 0   D T C  F O R T H   V I R T U A L   M A C H I N E
\ ===========================================================================

\ Define the FORTH virtual machine registers in terms of the TARGET machine registers

CROSS-COMPILING     \ these words will extend the functionality of the Cross-Assembler/Compiler

: TOS     R4  ;        \ TOP of stack is cached in R4
: SP      R6  ;        \ parameter stack pointer
: RP      R7  ;        \ return stack pointer
: W       R8  ;        \ Forth 'W' register OR general purpose in a system CODE word
: IP      R9  ;        \ Forth VM Instruction pointer
: NEXT    R10 ;        \ Forth's "NEXT" routine address cached in R10

\ Named registers defined in all the addressing modes
: *TOS          *R4  ;
: *TOS+         *R4+ ;
: (TOS)         (R4) ;

: *SP           *R6  ;
: *SP+          *R6+ ;
: (SP)          (R6) ;

: *RP           *R7  ;
: *RP+          *R7+ ;
: (RP)          (R7) ;

: *W            *R8  ;
: *W+           *R8+ ;
: (W)           (R8) ;

: *IP           *R9  ;
: *IP+          *R9+ ;
: (IP)          (R9) ;

: *NEXT         *R10 ;   \ we only use indirect addressing for this one


\ Create MAC ROs for Forth operations to simplify the code
: IP++         IP INCT, ;

: RP++         RP INCT, ;
: RP--         RP DECT, ;

: SP++         SP INCT, ;
: SP--         SP DECT, ;

\ PUSH & POP on both stacks
: PUSH,         ( src -- )  SP DECT,  *SP   MOV, ;    \ 10+18 = 28  cycles
: POP,          ( dst -- )  *SP+      SWAP  MOV, ;    \ 22 cycles

: DUP,          TOS PUSH, ;
: DROP,          TOS POP,  ;

: RPUSH,        ( src -- ) RP DECT,  *RP   MOV,  ;  \ 6 bytes
: RPOP,         ( dst -- ) *RP+      SWAP  MOV,  ;  \ 2 bytes

\ ========================================================================
\ N E S T E D   S U B - R O U T I N E   M A C R O
\ "Psuedo-instruction" to implements a CALL sub-routine mechanism on the
\ Forth return stack using the Branch & Link instruction.
\ You use CALL, exactly the same as you would use BL, but you can nest
\ sub-routine calls with CALL,

\ Return from CALL is done with psuedo instruction RT, just like BL,

\ See Xassembler   : RT,    *R11 B, ;             \ 12 cycles

: CALL,         ( dst -- )
                R11 RPUSH,       \ save R11 on forth return stack                 28
         ( addr | reg) BL,       \ branch & link saves the PC in R11             +16 =  44 to call
                 R11 RPOP, ;     \ R11 RPOP, is laid down by CALL, in the caller. 22+12= 34 to return
                                 \ We have to lay it in the code after BL so
                                 \ when we return from the Branch&link, R11 is
                                 \ restored to the original value from the rstack

\ ==========================================================================
\ D T C  F O R T H   I N N E R   I N T E R P R E T E R  "N E X T"
\ =========================================================================
\ This is that macro that runs at the end of CAMEL99 code words
\ It is equivalent to "return" for a Forth system
\ We can Branch to the code in 16bit RAM or compile it inline.

: JMP-NEXT  ( -- )   *NEXT B, ;               \ 4 bytes

\ INLINE NEXT
\ Although the code for DTC NEXT resides in 16 bit-RAM we access it via a
\ branch through R10. This adds and 16 cycles to NEXT.
: IL-NEXT   ( --- )  *IP+ W MOV,  *W B, ;    \ 8 bytes

\ ==========================================================================
\ RESOLVE NEXT, a deferred word
\ NEXT, is a deferred word in the cross-assembler so you can create different
\ threading mechanisms with the Cross-assembler easily.
\ We must assign an Execution token to "NEXT," so all the subsequent
\ CODE words will end with the correct code.

-1 
[IF]
  ' JMP-NEXT IS NEXT,         \ Assign JMP-NEXT to NEXT,
[ELSE]
  ' IL-NEXT IS NEXT,
 [THEN]


\ ==========================================================================
\ debugging macro.   Stops machine code in place
: BREAK    $$ JMP, ;
