\ FILES.F  handle base file extension for CAMEL99 Forth BJF 19MAR2018

\ Design:
\ We introduce the concept of a PAB Block. This is the PAB per TI specs
\ PLUS... the associated buffer, together as one data structure.
\ Since the disk system has 256 byte records max, we lock the buffer size
\ to 256 bytes.
\ The first PAB is create by the kernel and is at the very end of VDP RAM
\ New PABs are created above the pab moving LOWER in VDP memory.
\ Essentially a stack of pab blocks that can be accessed by 1 number.
\ The file handle is the number we use to calculate the address.
\ Effective address of a PAB block is  = 3EDF - (Handle * >120)

CR .( File Handle Generator)

\ VARIABLE HDL        \ last handle assigned by system
VARIABLE MAXFILES   \ most handles we can use

          8 CONSTANT LASTH    \ absolute maximum
       3EDF CONSTANT PAB-BASE
PSIZE 100 + CONSTANT PBLK     \ size of 1 pab block

\ array of PABs indexed by a handle
\ >> handle 0 is the KERNEL handle <<
CREATE PABS ( -- ) PAB-BASE , 0 , 0 , 0 , 0 , 0 , 0 , 0 , 0 ,

: ]PABS   ( ndx -- addr) 2* PABS + ;
: ]PABBLK ( ndx -- addr) PBLK * PAB-BASE SWAP - ;

\ assign pabblk to handle
\ this marks the handled as in use
: HUSED  ( Hndl -- ) DUP ]PABBLK  OVER ]PABS ! ;
: HFREE  ( Hndl -- )  ]PABS OFF ;

\ assign 'n' to a constant.
\ Used to re-assign value of PAB
: ->     ( n -- )  POSTPONE [']  POSTPONE >BODY  POSTPONE ! ; IMMEDIATE

: ?HANDLE ( n -- ) 0= ABORT" No more files" ;

: NEXTH   ( -- n )  \ give me a free handle to use.
          0               \ this is the fail flag
          MAXFILES @ 1+ 1
          DO
             I ]PABS @ 0=
             IF DROP I  LEAVE
             THEN
          LOOP
          DUP ?HANDLE
          DUP HUSED  ;

\ hi level API
: FSELECT ( handle -- ) ]PABS @ -> PAB ;

: FILES  ( n -- )
         DUP LASTH > ABORT" Max=8"
         MAXFILES ! ;

8 FILES 
CR .( Max files set to 8)



